plugins {
    id('java')
}

// See also:
// Based on https://docs.gradle.org/current/samples/sample_jvm_multi_project_with_additional_test_types.html
//def integrationTest = sourceSets.create('integrationTest')
//configurations[integrationTest.implementationConfigurationName].extendsFrom(configurations.testImplementation)
//configurations[integrationTest.runtimeOnlyConfigurationName].extendsFrom(configurations.testRuntimeOnly)

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom(implementation, testImplementation)
    integrationTestRuntimeOnly.extendsFrom(runtimeOnly, testRuntimeOnly)
}

dependencies {
    integrationTestImplementation 'org.junit.jupiter:junit-jupiter'
    integrationTestRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    shouldRunAfter(tasks.named('test'))

    useJUnitPlatform()

    environment("GEOFILES", '/opt/geosupport/current/fls/');
}

tasks.named('check') {
    dependsOn(integrationTest)
}
